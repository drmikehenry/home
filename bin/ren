#!/usr/bin/perl -w
#
# ren - larry's filename fixer
# small script from the "perl cookbook"
#
# examples: 	% ren 's/\.orig$//' *.orig
# 		% ren 'tr/A-Z/a-Z/ unless /^Make/' *
# 		% ren '$_ .= "bad"' *.f
# 		% ren 'print "$_: "; s/foo/bar/ if <STDIN> =~ /^y/i' *
# 		% find /tmp -name '*~' -print | ren 's/^(.+)~$/.#$1/'
use strict;

my $op = shift or die "Usage: ren expr [files]\n";
chomp(@ARGV = <STDIN>) unless @ARGV;
for (@ARGV) {
   my $was = $_;
   eval $op;
   die $@ if $@;
   rename($was,$_) unless $was eq $_;
}
